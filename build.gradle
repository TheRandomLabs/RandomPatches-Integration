buildscript {
	repositories {
		jcenter()

		maven {
			url "http://files.minecraftforge.net/maven"
		}
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "com.github.jengelman.gradle.plugins:shadow:2.0.0"
	}
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"

version = "1.12.2-1.5.1.0"
group = "com.therandomlabs.randompatches.integration"
archivesBaseName = "rpintegration"

sourceCompatibility = targetCompatibility = "1.8"

repositories {
	maven {
		url "https://jitpack.io"
	}

	maven {
		url "https://minecraft.curseforge.com/api/maven"
	}
}

dependencies {
	shadow "com.github.TheRandomLabs:RandomLib:1.12-SNAPSHOT"
	compile "com.github.TheRandomLabs:RandomLib:1.12-SNAPSHOT"
	deobfCompile "randompatches:randompatches:1.12.2:1.17.2.0"
	//Apparently doesn't work:
	//deobfCompile "morpheus:Morpheus:1.12.2:3.5.106"
	deobfCompile "muon:muon:0.2:1.12"
}

shadowJar {
	classifier = ""
	relocate "com.therandomlabs.randomlib", "com.therandomlabs.randompatches.integration.repackage.com.therandomlabs.randomlib"
	configurations = [project.configurations.shadow]
}

reobf {
	shadowJar {
		mappingType = "SEARGE"
	}
}

build.dependsOn shadowJar

artifacts {
	archives shadowJar
}

minecraft {
	version = "1.12.2-14.23.5.2838"
	runDir = "run"
	mappings = "stable_39"

	replace "@VERSION@", project.version
	replace "@FINGERPRINT@", project.findProperty("signSHA1")
}

processResources {
	//Ensure this task is redone when the versions change
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	//Replace version and mcversion in mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
}

jar {
	manifest {
		attributes "Manifest-Version": "1.0"
		attributes "FMLCorePlugin": "com.therandomlabs.randompatches.integration.core.RPICore"
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty("keyStore")
	}

	keyStore = project.findProperty("keyStore")
	alias = project.findProperty("keyStoreAlias")
	storePass = project.findProperty("keyStorePass")
	keyPass = project.findProperty("keyStoreKeyPass")
	inputFile = jar.archivePath
	outputFile = jar.archivePath
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = "deobf"
}

artifacts {
	archives deobfJar
}

build.dependsOn signJar
